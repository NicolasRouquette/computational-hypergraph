@dc:title "Computational Hypergraph Ontology"
@dc:type "Discipline"
@dc:creator "Systems Engineering & Formulation Division"
@dc:^description "The Computational Hypergraph Ontology defines concepts and properties for describing the topology of computational hypergraphs."
@dc:rights '''Copyright 2022, by the California Institute of Technology. ALL RIGHTS RESERVED. United States Government Sponsorship acknowledged. Any commercial use must be negotiated with the Office of Technology Transfer at the California Institute of Technology.

This software may be subject to U.S. export control laws. By accepting this software, the user agrees to comply with all applicable U.S. export laws and regulations. User has the responsibility to obtain export licenses, or other export authority as may be required before exporting such information to foreign countries or providing access to foreign persons.'''
vocabulary <http://caltech.edu/discipline/computational-hypergraph/computational-hypergraph#> as computational-hypergraph {
	
	extends <http://purl.org/dc/elements/1.1/> as dc
	extends <http://www.w3.org/2001/XMLSchema#> as xsd
	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs
	
  extends <http://mds.jpl.nasa.gov/sa/base#> as base
	
	aspect HypergraphElement :> base:ContainedElement
	
	@rdfs:label "Variable"
	concept Variable :> HypergraphElement, base:Container, base:ContainedElement [
	  restricts all relation base:contains to Variable
	]
	
	@rdfs:label "Triangle"
	concept Triangle :> HypergraphElement, base:Container [
		restricts all relation base:contains to HypergraphElement
	]
	
	aspect InputEdge :> HypergraphElement
	
	scalar property index [
		domain InputEdge
		range xsd:int
		functional
	]
	
	@rdfs:label "VariableInputEdge"
	relation entity VariableInputEdge :> InputEdge [
		from Variable
		to Triangle
		forward variableInput
		reverse inputFromVariable
	]
	
	@rdfs:label "TriangleInputEdge"
	relation entity TriangleInputEdge :> InputEdge [
		from Triangle
		to Triangle
		forward outputToTriangle
		reverse inputFromTriangle
		irreflexive
		asymmetric
	]
	
	@rdfs:label "OutputEdge"
	relation entity OutputEdge :> HypergraphElement [
		from Triangle
		to Variable
		forward output
		reverse outputOfTriangle
		functional
		inverse functional
	]
	
	scalar property mappingFunction [
		domain Triangle
		range xsd:string
		functional
	]
	
}